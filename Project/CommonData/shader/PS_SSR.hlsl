// ピクセルシェーダーの入力
struct PS_INPUT
{
    float4 Position : SV_POSITION; // 座標
    float4 DiffuseColor : COLOR0; // ディフューズカラー
    float4 SpecularColor : COLOR1; // スペキュラカラー
    float2 TextureCoord0 : TEXCOORD0; // テクスチャ座標０
    float2 TextureCoord1 : TEXCOORD1; // テクスチャ座標１
};

// ピクセルシェーダーの出力
struct PS_OUTPUT {
	float4 color0           : SV_TARGET0;	// 色
};

// 定数バッファピクセルシェーダー基本パラメータ
struct DX_D3D11_PS_CONST_BUFFER_BASE {
	float4		FactorColor;			// アルファ値等

	float		MulAlphaColor;			// カラーにアルファ値を乗算するかどうか( 0.0f:乗算しない  1.0f:乗算する )
	float		AlphaTestRef;			// アルファテストで使用する比較値
	float2		Padding1;

	int		    AlphaTestCmpMode;		// アルファテスト比較モード( DX_CMP_NEVER など )
	int3		Padding2;

	float4		IgnoreTextureColor;	// テクスチャカラー無視処理用カラー
};

// 基本パラメータ
cbuffer cbD3D11_CONST_BUFFER_PS_BASE				: register(b1) {
	DX_D3D11_PS_CONST_BUFFER_BASE		g_Base;
};


// プログラムとのやり取りのために使うレジスタ1
cbuffer cbMULTIPLYCOLOR_CBUFFER1 : register(b2)
{
    float2 dispsize;
}

// プログラムとのやり取りのために使うレジスタ
cbuffer cbMULTIPLYCOLOR_CBUFFER2 : register(b3) {
	float4 caminfo;
}


//テクスチャ
SamplerState g_Register0MapSampler : register(s0); // ディフューズマップサンプラ
Texture2D g_Register0MapTexture : register(t0); // ディフューズマップテクスチャ

SamplerState g_Register1MapSampler : register(s1); // 法線マップサンプラ
Texture2D g_Register1MapTexture : register(t1); // 法線マップテクスチャ

SamplerState g_Register2MapSampler : register(s2); // 深度マップサンプラ
Texture2D g_Register2MapTexture : register(t2); // 深度マップテクスチャ

//関数
float4 GetTexColor0(float2 texCoord, int2 offset = int2(0, 0)) {
    texCoord.y *= -1.f;
	return g_Register0MapTexture.Sample(g_Register0MapSampler, texCoord, offset);
}
float4 GetTexColor1(float2 texCoord, int2 offset = int2(0, 0)) {
	return g_Register1MapTexture.Sample(g_Register1MapSampler, texCoord, offset);
}
float4 GetTexColor2(float2 texCoord, int2 offset = int2(0, 0)) {
	return g_Register2MapTexture.Sample(g_Register2MapSampler, texCoord, offset);
}

float3 DisptoProjNorm(float2 screenUV) {
	float2 pos = screenUV;

	pos /= 0.5f;
	pos.x = pos.x - 1.f;
	pos.y = 1.f - pos.y;

	float3 position;
    position.x = pos.x * caminfo.z * dispsize.x / dispsize.y;
    position.y = pos.y * caminfo.z;
	position.z = 1.f;

	return position;
}

float2 ProjtoDisp(float3 position) {
	position = position / position.z;

	float2 screenUV;
	screenUV.x = position.x / caminfo.z * dispsize.y / dispsize.x;
	screenUV.y = position.y / caminfo.z;

	screenUV.x = screenUV.x + 1.f;
	screenUV.y = 1.f - screenUV.y;
	screenUV *= 0.5f;
	return screenUV;
}

bool Hitcheck(float3 position) {
	float2 screenUV = ProjtoDisp(position);
	if (
		(abs(screenUV.x) <= 1.f) &&
		(abs(screenUV.y) <= 1.f)
		) {
		float depth = GetTexColor2(screenUV).r;
		float z = depth / (caminfo.y * 0.005f);
        return (position.z < z && z < position.z + (caminfo.y * 1.0f));
    }
	else {
		return false;
	}
}

static float maxLength = 12.5f *100.f; // 反射最大距離
static int BinarySearchIterations = 24; //2分探索最大数

float4 applySSR(float3 normal, float2 screenUV) {
    float pixelStride;
    float3 delta;
    float3 position;

    float depth = GetTexColor2(screenUV).r;
    float z = depth / (caminfo.y * 0.005f);

    float4 color = float4(0.f, 0.f, 0.f, 0.f);

    bool IsCalc = (depth > 0.f);
    if (IsCalc)
    {
        float3 NormPos = DisptoProjNorm(screenUV);
		pixelStride = maxLength / caminfo.x;
		delta = reflect(NormPos, normal); // 反射ベクトル*１回で進む距離
        position = NormPos * z; //距離
    }
	[fastopt]
	for (int i = 0; i < caminfo.x; i++) {
        if (IsCalc) {
            position += delta * pixelStride;

            if (Hitcheck(position)) { //交差したので二分探索
                position -= delta * pixelStride; //元に戻し
                delta /= BinarySearchIterations; //進む量を下げる
    			[unroll] // attribute
                for (int j = 0; j < BinarySearchIterations; j++) {
                    if (IsCalc) {
                        pixelStride *= 0.5f;
                        position += delta * pixelStride;
                        if (Hitcheck(position)) {
                            pixelStride = -pixelStride;
                        }
                        if (length(pixelStride) < 1.f) {
                            IsCalc = false;
                        }
                    }
                }
				color = GetTexColor0(ProjtoDisp(position));// 交差したので色をブレンドする
                IsCalc = false;
            }
        }
    }
    return color;
}

PS_OUTPUT main(PS_INPUT PSInput) {
	//戻り値
	PS_OUTPUT PSOutput;
	//反射をどれだけ見せるか
    float uvFactor = 2.0 * length(PSInput.TextureCoord0 - float2(0.5, 0.5));
    uvFactor *= uvFactor;
    float edge = max(0.0, 1.0 - uvFactor);
    float Per = GetTexColor2(PSInput.TextureCoord0).g * edge;
	//ノーマル座標取得
    float3 normal = GetTexColor1(PSInput.TextureCoord0).xyz * 2.f - 1.f;
	
    float4 color = applySSR(normal, PSInput.TextureCoord0);
	//SSR
    if (Per > 0.f)
    {
        PSOutput.color0 = lerp(float4(0.f, 0.f, 0.f, 0.f), color, Per);
    }
    else
    {
        PSOutput.color0 = float4(0.f, 0.f, 0.f, 0.f);
    }
	//差分だけを出力する場合はこちら
    float3 Color = GetTexColor0(PSInput.TextureCoord0).xyz;
	if (
		PSOutput.color0.r == Color.r &&
		PSOutput.color0.g == Color.g &&
		PSOutput.color0.b == Color.b
		) {
		PSOutput.color0.a = 0.0;
	}
	return PSOutput;
}
